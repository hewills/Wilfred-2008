bool bridge_heater = false;
bool bridge_ac = false;
bool airlock_heater = false;
bool airlock_ac = false;
bool bridge_fans = false;
bool airlock_fans = false;

bool gravity = false;
bool airlock = false;
bool light1 = false;
bool light2 = false;
bool coffee = false;
bool tea = false;
bool cream = false;
bool water = false;

bool shipscan = false;
bool personscan = false;
bool repscan = false;


//----------------------------------------------------------------------------------------------------
// game_start
//----------------------------------------------------------------------------------------------------
function game_start() 
{
  
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute
//----------------------------------------------------------------------------------------------------
function repeatedly_execute()
{
  
   if (cBelt.Animating && willy_bridge2 && light1) {
     cWilfred.Walk(180, 190, eBlock, eWalkableAreas);
     willy_bridge3 = true;
     willy_bridge2 = false;
   }
   
   
   
   // logic for willy in airlock and AC is on
   
}

//----------------------------------------------------------------------------------------------------
// repeatedly_execute_always
//----------------------------------------------------------------------------------------------------
function repeatedly_execute_always() 
{
}

//----------------------------------------------------------------------------------------------------
// on_key_press
//----------------------------------------------------------------------------------------------------
function on_key_press(eKeyCode keycode) 
{
  if (IsGamePaused()) keycode = 0;
  
  // "System Keys"
  
  //Quit - Y/N
  if (keycode == eKeyEscape) {
   Mouse.SetBounds(0, 0, 320, 240);
   gExitGame.Visible = true;    
  }
    
  if (keycode == eKeyCtrlQ) QuitGame(0); // Ctrl-Q
  if (keycode == eKeyF9) RestartGame(); // F9
  if (keycode == eKeyF12) SaveScreenShot("scrnshot.pcx");  // F12
  
  // Debugger Keys
  if (keycode == eKeyCtrlS) Debug(0,0); // Ctrl-S, give all inventory
  if (keycode == eKeyCtrlV) Debug(1,0); // Ctrl-V, version
  if (keycode == eKeyCtrlA) Debug(2,0); // Ctrl-A, show walkable areas
  if (keycode == eKeyCtrlX) Debug(3,0); // Ctrl-X, teleport to room
}

// Quit the Game? GUI
// Click YES on Quit Game
function bQuitYes_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}
// Click NO on Quit
function bQuitNo_OnClick(GUIControl *control, MouseButton button)
{
  gExitGame.Visible = false;
}
// ------------------------------

//----------------------------------------------------------------------------------------------------
// on_mouse_click
//----------------------------------------------------------------------------------------------------
function on_mouse_click(MouseButton button)
{
	// all mouse clicks are handled in TwoClickHandler.asc!
}

//----------------------------------------------------------------------------------------------------
// on_event
//----------------------------------------------------------------------------------------------------
function on_event (EventType event, int data) 
{
}

// Intro Quit Button
function bQuit_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}
// Intro Start Button, Start Game!
function bStart_OnClick(GUIControl *control, MouseButton button)
{  
   aIntro.Stop();
   aOff.Play();
   cTeacher.ChangeRoom(98);
   cTeacher.Say("&1 Quiet down now children.");
   aSchool.Stop();
   aSchoolfade.Play();
   cTeacher.Say("&2 Today is the semicentennial of the heroic action performed by our school's namesake.");
   cTeacher.Say("&3 Yes, 50 years ago today, Wilfred Underfoot saved thousands aboard Flight 623.");
   cTeacher.Say("&4 And just this morning, the actual footage, taken from the ship's A.I., has been released to the public.");
   cTeacher.Say("&5 So children, pull out your tablets, and swipe to video link 523.");
   cTeacher.Say("&6 And let us watch the happenings of March 1, 2088.");
   
   gIntro.Visible = false;
   
   cSam.ChangeRoom(1);

}

//Pushed Warning Acknowledge Button
function bAck_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  gWarning.Visible = false;
  aSiren.Stop(); 
  cSam.Say("&1 Oh no!");
  cSam.Say("&2 Okay Samantha, this is what your AI was programmed for, you got this.");
  cSam.Say("&3 First! What's First?!");
}



// Looking at Wilfred
// Need logic for when he's doing something or in a certain location
function cWilfred_Look()
{
  if (willy_airlock) {
   cSam.Say("&4 Wilfred looks comfortable just where he is.");
  }
  
  if (willy_bridge1) {
   cSam.Say("&5 Wilfred is hiding in the shadows."); 
  }
  
  if (willy_bridge2) {
   cSam.Say("&6 Wilfred is watching for more cream."); 
  }
  
  if (willy_bridge3) {
   cSam.Say("&7 Come on kitty kitty. Jump on the button. Come on.. please?"); 
  }
  
  if (Mouse.Mode == eModeLaser && willy_bridge3) {    
   cSam.Say("&8 No sign of the toxin in Wilfred. His owners must have brought his food."); 
  }
  
  if (controls_on) {    
   cSam.Say("&9 Wilfred is one good kitty!"); 
  }
  
}


function dialog_request(int param) {
}


// --------------------
// GUI Panel of buttons
// --------------------

// Scan
function bScan_OnClick(GUIControl *control, MouseButton button)
{  aOff.Play();
   Mouse.SetPosition(100, 100);
   Mouse.SetBounds(70, 50, 300, 170);  
   gPanel.Visible = false;
   gScan.Visible = true;
}
// Crew
function bCrew_OnClick(GUIControl *control, MouseButton button)
{  aOff.Play();
   if (!scan_done) {
     cSam.Say("&10 I need to figure out what caused the inactivity warning first.");
   }
   else {
   Mouse.SetPosition(100, 100);
   Mouse.SetBounds(70, 50, 300, 170);  
   gPanel.Visible = false;
   gCrew.Visible = true;
   }
}
// Support
function bSupport_OnClick(GUIControl *control, MouseButton button)
{  aOff.Play();
   if (!scan_done) {
     cSam.Say("&10 I need to figure out what caused the inactivity warning first.");
   }
   else {
   Mouse.SetPosition(100, 100);
    Mouse.SetBounds(70, 50, 300, 170);
   gPanel.Visible = false;
   gSupport.Visible = true;
   }
}
// Systems
function bSystems_OnClick(GUIControl *control, MouseButton button)
{  aOff.Play();
   if (!scan_done) {
     cSam.Say("&10 I need to figure out what caused the inactivity warning first.");
   }
   else {
   Mouse.SetPosition(100, 100);
    Mouse.SetBounds(70, 50, 300, 170);
   gPanel.Visible = false;
   gSystems.Visible = true;
   }
}
//Controls
function bControls_OnClick(GUIControl *control, MouseButton button)
{  
   aOff.Play();
   if (!scan_done) {
     cSam.Say("&10 I need to figure out what caused the inactivity warning first.");
   }
   else {
     if (controls_on) {
        Mouse.SetPosition(100, 100);
         Mouse.SetBounds(70, 50, 300, 170);
        gPanel.Visible = false;
        gShipControls.Visible = true;
     }
     else cSam.Say("&11 I won't have control, until someone pushes the A.I. override button.");
   }
}
// ------- end of gui panel ---------------------



//Exit buttons on GUI ----------
function bExitScan_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gScan.Visible = false;
}
function bExitSupport_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gSupport.Visible = false;
}
function bExitCrew_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gCrew.Visible = false;
}
function bExitControls_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gShipControls.Visible = false;
}
function bExitSystems_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gSystems.Visible = false;
}
////-------------
function bExitBridge_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gBridge.Visible = false;
}
function bExitAirlock_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gAirlock.Visible = false;
}
function bExitProfile_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gProfile.Visible = false;
}
function bExitLights_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gLights.Visible = false;
}
function bExitReplicator_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  Mouse.SetBounds(0, 0, 320, 240);
  gReplicator.Visible = false;
}
function bExitInfo_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  gInfo.Visible = false;
}
// END OF GUI Exit buttons ------------------


// SCAN PANEL BUTTONS ----------------------------
function bShipscan_OnClick(GUIControl *control, MouseButton button)
{ aScan.Play();
  Display("Scanning...");
  
  Mouse.SetBounds(70, 50, 300, 170);
  lblInfo.Text = "Ship Integrity: Normal [  Ship Engines: OFF [ Ship Electric: Normal";
  gInfo.Visible = true;
  cSam.Say("&12 The engines have been powered down, otherwise everything looks optimal.");
  shipscan = true;
  if (shipscan && personscan && repscan) { scan_done = true; }
}

function bPersonnel_OnClick(GUIControl *control, MouseButton button)
{ aScan.Play();
  Display("Scanning...");
  cSam.Say("&13 Very strange.");
  cSam.Say("&14 All persons aboard are unconscious?");

  if (!personscan) {
    cSam.Say("&15 All but one.");
    cSam.Say("&16 Let me see if I can scan for the one conscious lifeform...");
    aScan.Play();
    Display("Lifeform Scan...");
    Display("** Lifeform Found **");
    lblInfo.Text = "Name: Wilfred Underfoot [ Age: 11 [ Species: Cat (Tabby) [ Credentials: 'Space-Walker Asst.' (Level 1) [ Mars Work Permit: 1 Year";
    gInfo.Visible = true;
    cSam.Say("&17 Found them!");
    Wait(200);
    cSam.Say("&18 A cat?! Belonging to a space walker.");
    cSam.Say("&19 The scan says he's in the Airlock corridor.");
  }

  cSam.Say("&20 This situation is sub par.");
  cSam.Say("&21 I can't instruct a cat on how to fly a ship.");
  cSam.Say("&22 And no one is awake to turn on my override controls. For want of a button push, the spaceship was lost.");
  cSam.Say("&24 Well, wait. A cat has paws. And mass. Couldn't it push the button? ");
  personscan = true;
  if (shipscan && personscan && repscan) { scan_done = true; }
}

function bSystem_OnClick(GUIControl *control, MouseButton button)
{ aScan.Play();
   Mouse.SetBounds(70, 50, 300, 170);
  
  if (!repscan) {
  lblInfo.Text = "Life Support: Normal [  Navigation: Mars Port [ Replicators: ** Error **";
  }
  else{
    lblInfo.Text = "Life Support: Normal [  Navigation: Mars Port [ Replicators: Normal";
  }
  
  gInfo.Visible = true;
  
  if (!repscan) {
    Wait(50);
    cSam.Say("&25 Hmm...the replicator scan failed.");
    cSam.Say("&26 I'll run a maintenance scan.");
    Wait(50);
    cSam.Say("&27 Someone has definitely tampered with the replicators, and laced everything with a toxin.");
    cSam.Say("&29 I'll run a maintenance fix, so at least it won't poison others.");
    repscan = true;
  }
  
  if (shipscan && personscan && repscan) { scan_done = true; }
}

function bManual_OnClick(GUIControl *control, MouseButton button)
{
  gScan.Visible = false;
  aReplicator.Play();
  Mouse.Mode = eModeLaser;
  cSam.Say("&31 This laser acts as a short range medical scan. It's more useful in medical bays.");
  Mouse.SetBounds(40, 175, 320, 212);
  
  if (willy_bridge3) {    
   cSam.Say("&32 The kitty is watching the laser carefully.");
  }
}
// --- End of Scan Panel buttons ----------------


// Support Panel ----------------
//Bridge
function bSupportBridge_OnClick(GUIControl *control, MouseButton button)
{ 
  aOff.Play();
  gSupport.Visible = false;
  gBridge.Visible = true;
}
//Airlock
function bSupportAirlock_OnClick(GUIControl *control, MouseButton button)
{ 
  aOff.Play();
  gSupport.Visible = false;
  gAirlock.Visible = true;
}
//----------------------------



// Crew Panel -------------------
//Capt. Jane
function bCrewCaptJane_OnClick(GUIControl *control, MouseButton button)
{
  aOff.Play();
  gCrew.Visible = false;
  gProfile.Visible = true;
  curr_crew = "Jane";
  
}
//Lt. Lorres
function bCrewLtLorres_OnClick(GUIControl *control, MouseButton button)
{
  aOff.Play();
  gCrew.Visible = false;
  gProfile.Visible = true;
  curr_crew = "Lorres";
}
//Lt. Wills
function bCrewLtWills_OnClick(GUIControl *control, MouseButton button)
{
  aOff.Play();
  gCrew.Visible = false;
  gProfile.Visible = true;
  curr_crew = "Wills";
}
//Ensign Cym
function bCrewEsnCym_OnClick(GUIControl *control, MouseButton button)
{
  aOff.Play();
  gCrew.Visible = false;
  gProfile.Visible = true;
  curr_crew = "Cym";
}
// --- End of Crew Panel --------------

// Systems Panel ----------------------
function bSystemsLights_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  gSystems.Visible = false;
  gLights.Visible = true;
}

function bSystemsReplicator_OnClick(GUIControl *control, MouseButton button)
{ aOff.Play();
  gSystems.Visible = false;
  gReplicator.Visible = true;
}
// --- End of Systems Panel ------------


// Ship Controls Panel buttons -----------------------
function bControlsEngines_OnClick(GUIControl *control, MouseButton button)
{
  if (!engines_on) {
    aOff.Play();
    bControlsEngines.Text = "Engines (on)";
    aEngineStart.Play(eAudioPriorityHigh, eOnce);
    engines_on = true;
    cSam.Say("&33 Now we're cooking!");
    
    Wait(30);
    aAirlock.Play();
    cHacker.ChangeRoom(1);
    cHacker.Animate(0, 3, eOnce, eBlock);
    cHacker.ChangeView(15);
    aHacker.Play(eAudioPriorityVeryHigh, eRepeat);
    cSam.Say("&34 Who?! What?! Sh**.");
    cSam.Say("&35 Anyone hanging outside of the ship has got to up to something.");
    cSam.Say("&36 Ow! They're removing my access! Jerk!");
    
    bControlsGravity.Visible = false;
    bControlsEngines.Visible = false;
    bScan.Visible = false;
    bCrew.Visible = false;
    
     Mouse.SetBounds(0, 0, 320, 240);
    
    
    cWilfred.ChangeView(16);
    cSam.Say("&38 There goes the gravity. Wilfred!");
    gShipControls.Visible = false;
    
    cWilfred.SetWalkSpeed(2, 2);
    cWilfred.Walk(131, 25, eBlock, eAnywhere);
    cWilfred.Move(131, 70, eBlock, eAnywhere);
    cWilfred.ChangeView(17);
    cWilfred.Animate(0, 20, eRepeat, eNoBlock);
    cWilfred.SetIdleView(17, 20);
    
    cSam.Say("&39 Way to go Wilfred! His space walker training taught him to head towards the spacesuit.");
    cSam.Say("&23 Or just blind luck.");
   
    if (Mouse.Mode == eModeLaser) {    
      Mouse.Mode = eModeLookat;
    }
    
  }
  else cSam.Say("&40 I actually managed to get a cat to jump onto a button. There is no way in hell I'm turning these engines off.");
}

function bControlsGravity_OnClick(GUIControl *control, MouseButton button)
{
  cSam.Say("&41 As funny as it is to see things floating around. I better not.");
}

function bControlsNav_OnClick(GUIControl *control, MouseButton button)
{
  cSam.Say("&42 We are already on our way to the Mars Space Port.");
}

function bControlsAirlock_OnClick(GUIControl *control, MouseButton button)
{
  if (!engines_on) {
    cSam.Say("&43 Wait! I don't want to suck Wilfred out into space!");
  }
  else
  {
    gShipControls.Visible = false;
    Mouse.SetBounds(0, 0, 320, 240);
    cSam.Say("&44 Hold on Wilfred! It's time to take out the garbage!");
    aHacker.Stop();
    aAirlock.Play();
    aScream.Play();
    cHacker.ChangeView(15);
    cHacker.Animate(1, 20, eOnce, eBlock);
    cHacker.Animate(2, 20, eOnce, eNoBlock);
    cHacker.ChangeRoom(98);
    
    Wait(20);
    cSam.Say("&45 Hey cat! Ready to go?");
    
    aWilfred_meow3.Play(eAudioPriorityHigh);
    Wait(10);
    
    cSam.Say("&46 Me too."); 
    
    Wait(30);
    
    if (aShip_ambiance != null) {
    aShip_ambiance.Stop();
    }
    
    if (aFans != null) {
    aFans.Stop();
    }
    
    if (aEnginesRunning != null) {
    aEnginesRunning.Stop();
    }
    
    cTeacher.ChangeRoom(99, 164, 117);
    cSam.ChangeRoom(99);
   
  }
}
//---- End of Ship Control buttons ------------------


// Ship Support Panel buttons --------------------
// BRIDGE HEATER
function bBridgeHeater_OnClick(GUIControl *control, MouseButton button)
{
  //Turn heater off
  if (bridge_heater && !bridge_ac) {
    aOff.Play();
    aHeater.Stop();
    bridge_heater = false;
    bBridgeHeater.Text = "Heater (off)";
    cSam.Say("&47 The Bridge is cooling back down.");
  }
  else if (!bridge_heater && bridge_ac) {
    cSam.Say("&48 I shouldn't turn the heater on, when the AC is on.");
  }//Turn heater on
  else if (!bridge_heater && !bridge_ac) {
    aOff.Play();
    aHeater.Play(eAudioPriorityNormal, eRepeat);
    bridge_heater = true;
    bBridgeHeater.Text = "Heater (on)";
    cSam.Say("&49 The Bridge is warming up.");
  }
  else {
   cSam.Say("&50 I'm not sure what I'm doing."); 
  }
}
//BRIDGE AC
function bBridgeAC_OnClick(GUIControl *control, MouseButton button)
{
  if (bridge_ac && !bridge_heater) {
    aOff.Play();
    aHeater.Stop();
    bridge_ac = false;
    bBridgeAC.Text = "AC (off)";
    //Set button text here or when opening GUI
    cSam.Say("&51 The Bridge is heating back up.");
  }
  else if (!bridge_ac && bridge_heater) {
    cSam.Say("&52 I shouldn't turn the AC on, when the heater is already on.");
  }
  else if (!bridge_ac && !bridge_heater) {
    aOff.Play();
    aHeater.Play(eAudioPriorityNormal, eRepeat);
    bridge_ac = true;
    bBridgeAC.Text = "AC (on)";
    cSam.Say("&53 The Bridge is freezing.");
  }
  else {
   cSam.Say("&50 I'm not sure what I'm doing."); 
  }
}
//BRIDGE FANS
function bBridgeFans_OnClick(GUIControl *control, MouseButton button)
{
   //Fans off
   if (bridge_fans) {
    aOff.Play();
    aFans.Stop();
    bridge_fans = false;
    bBridgeFans.Text = "Fans (off)";
    cSam.Say("&54 The Bridge fans have stopped blowing.");
    cBelt.ChangeView(1);
  }// Fans on
  else {
    aOff.Play();
    aFans.Play(eAudioPriorityHigh, eRepeat);
    bridge_fans = true;
    bBridgeFans.Text = "Fans (on)";
    cSam.Say("&55 The Bridge fans are blowing.");
    cBelt.ChangeView(10);
    cBelt.Animate(0, 5, eRepeat, eNoBlock, eForwards);
    
    if (willy_bridge2) {      
     cSam.Say("&56 Wilfred is fascinated with the moving seatbelt.");
    }
  }
  
  if (cBelt.Animating && willy_bridge2 && light1) {
     cWilfred.Walk(180, 190, eBlock, eWalkableAreas);
     willy_bridge3 = true;
     willy_bridge2 = false;
  }
}

function bBridgeOxygen_OnClick(GUIControl *control, MouseButton button)
{
  cSam.Say("&57 Turning off the oxygen would kill the crew. My AI tells me that's not a good idea.");
}

function bAirlockHeater_OnClick(GUIControl *control, MouseButton button)
{
  if (airlock_heater && !airlock_ac) {
    aOff.Play();
    airlock_heater = false;
    bAirlockHeater.Text = "Heater (off)";
    cSam.Say("&58 The airlock room is cooling back down.");
  }
  else if (!airlock_heater && airlock_ac) {
    cSam.Say("&48 I shouldn't turn the heater on, when the AC is already on.");
  }
  else if (!airlock_heater && !airlock_ac) {
    aOff.Play();
    airlock_heater = true;
    bAirlockHeater.Text = "Heater (on)";
    cSam.Say("&59 The airlock room is warming up.");
        
    if (willy_airlock) {      
      cSam.Say("&60 Wilfred seems to be enjoying the extra heat.");
    }
    
  }
  else {
   cSam.Say("&50 I'm not sure what I'm doing."); 
  }
}

function bAirlockAC_OnClick(GUIControl *control, MouseButton button)
{
  if (airlock_ac && !airlock_heater) {
    aOff.Play();
    airlock_ac = false;
    //Airlock AC off
    bAirlockAC.Text = "AC (off)";
    cSam.Say("&61 The airlock room is heating back up.");
  }
  else if (!airlock_ac && airlock_heater) {
    cSam.Say("&52 I shouldn't turn the AC on, when the heater is already on.");
  }
  else if (!airlock_ac && !airlock_heater) {
    aOff.Play();
    airlock_ac = true;
    //Airlock AC on
    bAirlockAC.Text = "AC (on)";
    cSam.Say("&62 The airlock room is freezing.");
    
    if (willy_airlock) {      
      cSam.Say("&63 Wilfred doesn't like the cold AC air.");
      //Wilfred moves out of the cold
      cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
      cSam.Say("&64 Yes! He's moved into the Bridge. But he's sticking to the shadows.");
      willy_bridge1 = true;
      willy_airlock = false;
    }
  }
  else {
   cSam.Say("&50 I'm not sure what I'm doing."); 
  }
}

function bAirlockFans_OnClick(GUIControl *control, MouseButton button)
{
  if (airlock_fans) {
    airlock_fans = false;
    aOff.Play();
    //Airlock Fans off
    bAirlockFans.Text = "Fans (off)";
    cSam.Say("&65 The airlock fans have stopped blowing.");
  }
  else {
    aOff.Play();
    airlock_fans = true;
    //Airlock Fans on
    bAirlockFans.Text = "Fans (on)";
    cSam.Say("&66 The airlock fans are blowing.");
  }
}

function bAirlockOxygen_OnClick(GUIControl *control, MouseButton button)
{
   cSam.Say("&67 Turning off the oxygen is generally a bad idea.");
}
//--- End of Support Panel buttons ----------------



// LIGHTS ---------------

//Crew Panel Light (willy_bridge3)
function bLight1_OnClick(GUIControl *control, MouseButton button)
{
  //Turn on Light #1 (Crew Panel)
  if (!light1) {
    
   aOff.Play();
   
   light1 = true;
   light2 = false;
   bLight1.Text = "Bridge 1 (on)";
   bLight2.Text = "Bridge 2 (off)";
   Mouse.SetBounds(0, 0, 320, 240);
   
   gLights.Visible = false;
   region[2].Enabled = true;
   region[3].Enabled = false;
  
   SetBackgroundFrame(2);
   
   if (cBelt.Animating && willy_bridge2 && light1) {
     cWilfred.Walk(180, 190, eBlock, eWalkableAreas);
     willy_bridge3 = true;
     willy_bridge2 = false;
   }
   else if (willy_bridge2 && light1 && !cBelt.Animating) {
     cSam.Say("I need to get Wilfred to walk towards the shadow closer to Lt. Lorres.");
   }
   
  }
  else if (light1) {
    aOff.Play();
    light1 = false;
    bLight1.Text = "Bridge 1 (off)";
    Mouse.SetBounds(0, 0, 320, 240);
    gLights.Visible = false;
    region[2].Enabled = false;
    SetBackgroundFrame(0);
  }
  
  if (willy_bridge3 && !light1) {
   cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
   cSam.Say("&68 Shoot! Wilfred got scared being out of the shadows.");
   willy_bridge3 = false;
   willy_bridge2 = false;
   willy_bridge1 = true;
  }

  if (willy_bridge2 && !light2) {
   cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
   cSam.Say("&68 Shoot! Wilfred got scared being out of the shadows.");  
   willy_bridge3 = false;
   willy_bridge2 = false;
   willy_bridge1 = true;
  }  

}

// Replicator Light (willy_bridge2)
function bLight2_OnClick(GUIControl *control, MouseButton button)
{
  //Turn on Light #2
  //Changing background and Region enabling
  if (!light2) {
   
   aOff.Play();
      
   light2 = true;
   light1 = false;
   bLight2.Text = "Bridge 2 (on)";
   bLight1.Text = "Bridge 1 (off)";
   Mouse.SetBounds(0, 0, 320, 240);
   
   gLights.Visible = false;
   region[3].Enabled = true;
   region[2].Enabled = false;
   
   SetBackgroundFrame(1);
  }
  else if (light2) {
    aOff.Play();
    light2 = false;
    bLight2.Text = "Bridge 2 (off)";
    Mouse.SetBounds(0, 0, 320, 240);
    gLights.Visible = false;
    region[3].Enabled = false;
    SetBackgroundFrame(0);
  }
  
  if (willy_bridge3 && !light1) {
   cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
   cSam.Say("&68 Shoot! Wilfred got scared being out of the shadows."); 
   willy_bridge3 = false;
   willy_bridge2 = false;
   willy_bridge1 = true;
  }
    
   if (willy_bridge2 && !light2) {
   cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
   cSam.Say("&68 Shoot! Wilfred got scared being out of the shadows.");
   willy_bridge3 = false;
   willy_bridge2 = false;
   willy_bridge1 = true;
  }
}
//---- End of Lights ---



// Crew Credits ---------------
function bCredits_OnClick(GUIControl *control, MouseButton button)
{
  if (curr_crew == "Jane") {
    
    gProfile.Visible = false;
    lblCredits.Text = "9,433 Credits";
    gCredits.Visible = true;
  }
  
  if (curr_crew == "Lorres") {
    
    gProfile.Visible = false;
    lblCredits.Text = "1,830 Credits";
    gCredits.Visible = true;
  }
  
  if (curr_crew == "Wills") {
    
    gProfile.Visible = false;
    lblCredits.Text = "900 Credits";
    gCredits.Visible = true;
  }
  
  if (curr_crew == "Cym") {
    
    gProfile.Visible = false;
    lblCredits.Text = "-26 Credits";
    gCredits.Visible = true;
  }
}
//--End Crew Credits ---------------

//Click OK on Credits Panel
function bOK_OnClick(GUIControl *control, MouseButton button)
{  Mouse.SetBounds(0, 0, 320, 240);  //Release bounds
   gProfile.Visible = true;
   gCredits.Visible = false;
   lblCredits.Text = "";
}


// Crew Playlists --------------
function bPlaylist_OnClick(GUIControl *control, MouseButton button)
{ 
  if (curr_crew == "Jane") {
    
    gProfile.Visible = false;
    Mouse.SetBounds(0, 0, 320, 240);
    aCountry.Play(eAudioPriorityHigh, eOnce);
    
  }
  
  if (curr_crew == "Lorres") {
    
    gProfile.Visible = false;
    Mouse.SetBounds(0, 0, 320, 240);
    aBossanova.Play(eAudioPriorityHigh, eOnce);
  }
  
  if (curr_crew == "Wills") {
    
    gProfile.Visible = false;
    Mouse.SetBounds(0, 0, 320, 240);
    aSamovar.Play(eAudioPriorityHigh, eOnce);
  }
  
  if (curr_crew == "Cym") {
    
    AudioChannel* dubstep = aDubstep.Play(eAudioPriorityHigh, eOnce);
 
    gProfile.Visible = false;
    Mouse.SetBounds(0, 0, 320, 240);

    while (dubstep.IsPlaying) {
      Wait(1);
    }
    
    aWilfred_angry.Play(eAudioPriorityHigh, eOnce);
    
    if (!willy_hiding) {
      cWilfred.ChangeView(6);
      cWilfred.Animate(0, 3, eOnce, eBlock, eForwards);
      cWilfred.ChangeView(3);
    }
    
    aMusic.Play(eAudioPriorityNormal, eRepeat);
    
    if (willy_hiding) {
      cWilfred.ChangeRoom(1, 54, 44,  eDirectionDown);
      cWilfred.Say("&1 MRROOOOWWW!");
      aWilfred_hiss.Play(eAudioPriorityHigh, eOnce);
      
      cWilfred.ChangeView(6);
      cWilfred.Animate(0, 3, eOnce, eBlock, eForwards);
      cWilfred.ChangeView(3);
      
      cWilfred.Say("&2 Hisssss");
      willy_hiding = false;
      willy_airlock = true;
      cSam.Say("&69 I don't think Wilfred cared for the dubstep music. He's left his hiding place.");
      
      if (airlock_ac) {      
      cSam.Say("&63 Wilfred doesn't like the cold AC air.");
      //Wilfred moves out of the cold
      cWilfred.Walk(95, 72, eBlock, eWalkableAreas);
      cSam.Say("&64 Yes! He's moved into the Bridge. But he's sticking to the shadows.");
      willy_bridge1 = true;
      willy_airlock = false;
      }
      
    }
  }
}
//--End Crew Playlists ------------

//Replicator Buttons

function bCream_OnClick(GUIControl *control, MouseButton button)
{
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  //Play cream flowing
  cRep.ChangeView(7);
  cRep.Animate(0, 5, eOnce, eBlock, eForwards);
  cRep.ChangeView(1);
  
  if (!light2 && willy_bridge1) {
   aWilfred_meow3.Play(eAudioPriorityHigh, eOnce);
   cSam.Say("&71 Wilfred sees the cream, but I think he's scared to come out into the light.");
  }
  
  if (light2 && willy_bridge1) {
   cWilfred.Walk(280, 76, eBlock, eWalkableAreas);
   cSam.Say("&72 Wilfred likes the look of that cream!");
   willy_bridge1 = false;
   willy_bridge2 = true;
  }
  
  if (willy_bridge2 && cBelt.Animating == true) {      
     cSam.Say("&56 Willy is fascinated with the moving seatbelt.");
  }
    
}

function bCoffee_OnClick(GUIControl *control, MouseButton button)
{
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  //Play cream flowing
  cRep.ChangeView(8);
  cRep.Animate(0, 5, eOnce, eBlock, eForwards);
  cRep.ChangeView(1);
}

function bTea_OnClick(GUIControl *control, MouseButton button)
{
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  //Play cream flowing
  cRep.ChangeView(9);
  cRep.Animate(0, 5, eOnce, eBlock, eForwards);
  cRep.ChangeView(1);
}

function bWater_OnClick(GUIControl *control, MouseButton button)
{
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  aReplicator.Play(eAudioPriorityHigh, eOnce);
  
  //Play cream flowing
  cRep.ChangeView(2);
  cRep.Animate(0, 5, eOnce, eBlock, eForwards);
  cRep.ChangeView(1);
}



function cBelt_Look()
{
  cSam.Say("&73 The end of a seatbelt.");
}

function cHacker_Look()
{
  cSam.Say("&74 They are trying to shut me down!");
}
